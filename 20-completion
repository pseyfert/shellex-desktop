# vim:ft=zsh
# Configure tab completion and make zsh suggest .desktop files
# Â© 2017 Paul Seyfert and contributors (see also: LICENSE)

_desk() {
  local -a apps
  apps=()
  # collect .desktop files
  for app in /usr/share/applications/*.desktop(:s/.desktop//:s#/usr/share/applications/##)
  do
    apps+=($app)
  done
  # -P adds a prefix to all words.
  # a) user types "Thunar<Tab>" and gets "gtk-launch=Thunar"
  # b) user types "gtk-<TAB>" and gets "gtk-launch="
  #
  # the actual command should be 'gtk-launch Thunar' for .desktop files
  # but the whitespace would be a problem for completion.
  # Catch that later in zle
  _wanted commands expl "desktop files" compadd -P "gtk-launch=" "$apps[@]"
  # TODO suggest "Name=" fields from .desktop files instead of .desktop file name
}

_run() {
  local -a alts
  alts=()
  alts+=('desk:desktop files:_desk')
  alts+=('cmd:regular commands:_command_names')
  _alternative "$alts[@]"
}

autoload -Uz compinit
compinit

# force loading of _normal
# http://unix.stackexchange.com/a/335868/150443
autoload -Uz +X _normal
# store _normal in original_normal
functions[original_normal]=$functions[_normal]

# define a new top-level completion
_normal() {
  if [[ $CURRENT == 1 ]] ; then _run ; else original_normal ; fi
  # TODO restore original_normal to normal after the first argument is done
  # (in normal shell mode we don't want other completion functions to call
  # our _normal instead of the original
}

setopt +o bashautolist
setopt +o listambiguous
setopt +o autolist

zstyle ':completion:*:default' menu select=0
zstyle ':completion:*:default' compstate last_prompt=1
zstyle ':completion:*:default' select-scroll 0

